#version 450 core

#include <Misc.glsl>

layout(local_size_x = 32, local_size_y = 32) in;

layout(set = 0, binding = 0) uniform sampler2D last_level;
layout(set = 0, binding = 1, rg32f) writeonly uniform image2D cur_level;

float reduce_max(float a, float b) {
    return max(a, b);
}

float reduce_min(float a, float b) {
    return min(a, b);
}

#define R4V(r, v) r(v.x, r(v.y, r(v.z, v.w)))
#define R3(r, a, b, c) r(a, r(b, c))

void main() {
    ivec2 index = ivec2(gl_GlobalInvocationID.xy);
    ivec2 cur_level_size = imageSize(cur_level);
    ivec2 last_level_size = textureSize(last_level, 0);

    if (index.x >= cur_level_size.x || index.y >= cur_level_size.y) {
        return;
    }

    ivec2 index_in_last_level = index * 2;
    vec2 last_level_grid_size = 1.0 / last_level_size;
    vec2 last_level_uv = (index_in_last_level + 1) * last_level_grid_size;

    vec4 maxes = textureGather(last_level, last_level_uv, 0);
    float max_depth01 = R4V(reduce_max, maxes);
    vec4 mins = textureGather(last_level, last_level_uv, 1);
    float min_depth01 = R4V(reduce_min, mins);

    // For NPOT(none power of two) texture
    bool pad_width = (cur_level_size.x * 2 < last_level_size.x) &&
                     (index.x + 1 == cur_level_size.x);
    bool pad_height = (cur_level_size.y * 2 < last_level_size.y) &&
                      (index.y + 1 == cur_level_size.y);

    if (pad_width) {
#define PAD(r, c, o)                                                           \
    vec2 r##s =                                                                \
        textureGather(                                                         \
            last_level, last_level_uv + vec2(last_level_grid_size.x, 0.0), c)  \
            .yz;                                                               \
    o = R3(r, r##s.x, r##s.y, o)

        PAD(reduce_max, 0, max_depth01);
        PAD(reduce_min, 1, min_depth01);

#undef PAD
    }

    if (pad_height) {
#define PAD(r, c, o)                                                           \
    vec2 r##s =                                                                \
        textureGather(                                                         \
            last_level, last_level_uv + vec2(0.0, last_level_grid_size.y), c)  \
            .xy;                                                               \
    o = R3(r, r##s.x, r##s.y, o)

        PAD(reduce_max, 0, max_depth01);
        PAD(reduce_min, 1, min_depth01);

#undef PAD
    }

    if (pad_width && pad_height) {
#define PAD(r, c, o)                                                           \
    float r##s =                                                               \
        texture(last_level, last_level_uv + 1.5 * last_level_grid_size).c;     \
    o = r(r##s, o)

        PAD(reduce_max, r, max_depth01);
        PAD(reduce_min, g, min_depth01);

#undef PAD
    }

    imageStore(cur_level, index, vec4(max_depth01, min_depth01, 0, 0));
}
